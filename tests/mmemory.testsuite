## -*- tcl -*-
## (c) 2013 Andreas Kupries
# # ## ### ##### ######## ############# #####################
## Test suite for memory stores, custom parts.
#
## The external procedures 'already' and 'badmethod' are used to
## generate implementation specific error messages. Implementations
## have to be provided by the invoking implementation-specific
## testsuite.
#
## The external procedures 'new-store' and 'release-store' are used to
## create and destroy aqn instance of the store under test.

# # ## ### ##### ######## ############# #####################

test mphash-memory-1.0 {new, wrong#args, too many} -body {
    mphash::memory new X
} -returnCodes error -result {wrong # args: should be "mphash::memory new"}

test mphash-memory-1.1 {create, wrong#args, too many} -body {
    mphash::memory create foo X
} -returnCodes error -result {wrong # args: should be "mphash::memory create foo"}

test mphash-memory-1.2 {constructor, create, existing command} -setup {
    mphash::memory create myphmem
} -body {
    mphash::memory create myphmem
} -cleanup {
    myphmem destroy
} -returnCodes error -result [already myphmem]

test mphash-memory-1.3 {constructor, create, existing command} -body {
    mphash::memory create set
} -returnCodes error -result [already set]

test mphash-memory-1.4 {constructor, create} -setup {
    set result {}
} -body {
    lappend result [mphash::memory create myphmem]
    lappend result [info commands ::myphmem]
    lappend result [myphmem size]
} -cleanup {
    myphmem destroy
    unset result
} -result {::myphmem ::myphmem 0}

test mphash-memory-1.5 {constructor, new} -setup {
    set result {}
} -body {
    lappend result [set foo [mphash::memory new]]
    lappend result [info commands ::$foo]
    lappend result [$foo size]
} -cleanup {
    $foo destroy
    unset result foo
} -match glob -result {::oo::Obj* ::oo::Obj* 0}

# # ## ### ##### ######## ############# #####################
return
